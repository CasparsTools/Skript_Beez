command /rules [<text>]:
	trigger:
		send "&e[Beez Server Rules]%nl%%nl%&e1. &fAny kind of &eblacklisted modifications &fthat gives &eunfair advantages &fis not allowed, commonly known as &echeating&f.%nl%&e2. &fAny way of &eflooding server chat &fwith meaningless messages or include &eracism&f, &esexism&f, &eharassment &fcontent is not allowed.%nl%&e3. &fConnect to the server with &eanother account after being prohibited &fwill result in punishment length extension.%nl%&e4. &fAbusing &eglitches &for &espreading the instructions &fare not allowed, please &ereport bugs &fon our discord server or to a staff member.%nl%%nl%&e&lNOTE: &eBadlion Client&f, &eLunar Client&f, &eLounge Client&f, and other &eclient-based anti-cheat &fwill &cNOT &fbe considered when we suspend a user for rule ##1!%nl%" to executor

command /itemShop [<text>] [<text>]:
	trigger:
		if arg 2 is not "NPC_CLICK":
			send "&cYou are not allowed to use this command!" to executor
			stop
		game_shopHandler_itemShop_gui(executor, (arg 1))

command /bank [<text>] [<text>]:
	trigger:
		if arg 2 is not "NPC_CLICK":
			send "&cYou are not allowed to use this command!" to executor
			stop
		game_bankHandler_gui(executor, (arg 1))

command /quest [<text>] [<text>]:
	trigger:
		if arg 2 is not "NPC_CLICK":
			send "&cYou are not allowed to use this command!" to executor
			stop
		game_questHandler_gui(executor, (arg 1))

command /cancelQuest [<text>]:
	trigger:
		if {-game_questHandler::%executor%_ACTIVE_QUEST} is not set:
			send "&cYou don't have any active quest!" to executor
			stop
		game_questHandler_guiAction(executor, "NULL", "CANCEL_QUEST")

command /event [<text>]:
	trigger:
		if arg 1 is not set:
			send "&cUsage: /event join" to executor
			stop
		if arg 1 is not "join":
			send "&cUsage: /event join" to executor
			stop
		set {_executorUUID} to (executor's UUID)
		set {_latestEvent} to {-game_randomEventHandler::LATEST_EVENT}
		if {-game_randomEventHandler::%{_latestEvent}%_BOSS_FIGHT_QUEUE} is not set:
			send "&cThe event queue is no longer joinable!" to executor
			stop
		if {-game_randomEventHandler::%{_latestEvent}%_BOSS_FIGHT_PLAYERS::%{_executorUUID}%} is set:
			send "&cYou have already joined the event!" to executor
			stop
		set {-game_randomEventHandler::%{_latestEvent}%_BOSS_FIGHT_PLAYERS::%{_executorUUID}%} to executor
		send "&bYou have joined the event!" to executor

command /myStats [<text>]:
	trigger:
		send "&e[Player Stats]%nl%&f%nl%&eHealth: &f%{playerData::%executor%_HEALTH}%&e/&f%{playerData::%executor%_HEALTH_MAX}%%nl%&eRegeneration: &f%ceil({playerData::%executor%_HEALTH_MAX} * ({playerData::%executor%_HEALTH_REGENERATION} / 100))%/sec &e(&f%{playerData::%executor%_HEALTH_REGENERATION}%%%&e)%nl%&eMelee Damage: &f%{playerData::%executor%_MELEE_DAMAGE}%%nl%&eRanged Damage: &f%{playerData::%executor%_RANGED_DAMAGE}%%nl%&eMagic Damage: &f%{playerData::%executor%_MAGIC_DAMAGE}%%nl%&eProtection: &f%{playerData::%executor%_PROTECTION}%%%%nl%&eMovement Speed: &f%(({playerData::%executor%_MOVEMENT_SPEED} / 0.2) * 100)%%%%nl%&eCritical Chance: &f%{playerData::%executor%_CRITICAL_CHANCE}%%%%nl%&eCritical Power: &f%{playerData::%executor%_CRITICAL_POWER}%%%%nl%&eArrow Return: &f%{playerData::%executor%_ARROW_RETURN}%%%" to executor

command /updatePlayer [<text>]:
	trigger:
		if {-game_eventHandler::%executor%_UPDATE_PLAYER} is set:
			send "&cPlease wait for the previous update player process to complete!" to executor
			stop
		set {-game_eventHandler::%executor%_UPDATE_PLAYER} to now
		send "&aReloading player in-game data..." to executor
		game_playerInventoryHandler_saveInventory(executor)		
		wait 5 ticks
		game_playerInventoryHandler_loadInventory(executor)
		wait 5 ticks
		send "&aReloading player display settings..." to executor
		game_healthbarHandler_new(executor)
		wait 5 ticks
		game_healthbarHandler_update(executor)
		wait 5 ticks
		game_equipmentHandler_updatePlayerEquipmentStats(executor)
		wait 5 ticks
		game_playerDisplayHandler_sidebar_newPlayerSidebar(executor, 0)
		wait 5 ticks
		game_playerDisplayHandler_sidebar_updatePlayerSidebar(executor)
		wait 5 ticks
		game_invisiblePressurePlateHandler_hideAllFromPlayer(executor)
		game_playerDisplayHandler_playerTablist(executor)
		send "&aReload completed!" to executor
		delete {-game_eventHandler::%executor%_UPDATE_PLAYER}

command /redeemCode [<text>]:
	trigger:
		if arg 1 is not set:
			send "&cUsage: /redeemCode <code>" to executor
			stop
		set {_code} to (arg 1)
		set {_playerCodeStatus} to game_codeHandler_checkPlayerCode(executor, {_code})
		if {_playerCodeStatus} is "CODE_ALREADY_REDEEMED":
			send "&cYou have already redeemed this code!" to executor
			stop
		else if {_playerCodeStatus} is "CODE_NOT_EXIST":
			send "&cInvalid Code!" to executor
			stop
		else if {_playerCodeStatus} is "CODE_EXPIRED":
			send "&cThis code has already expired!" to executor
			stop
		game_codeHandler_playerRedeemCode(executor, {_code})
		send "&aYou have successfully redeemed the code!" to executor

command /getItem [<text>] [<number>]:
	trigger:	
		if executor is not a op:
			send "&cYou are not allowed to use this command!" to executor
			stop
		if arg 1 is not set:
			send "&cUsage: /getItem <ID> [amount]" to executor
			stop
		set {_targetItem} to {-game_dataHandler::%arg 1%}
		if {_targetItem} is not set:
			send "&cNo item found for this ID!" to executor
			stop
		if arg 2 is set:
			if (arg 2) <= 0:
				send "&cAmount need to be at least 1!" to executor
				stop
			give ((arg 2) of {_targetItem}) to ("%executor%" parsed as player)
		else:
			give {_targetItem} to ("%executor%" parsed as player)
		send "&aAdded item to your inventory!" to executor
		game_logHandler_sendLog("%executor% spawned in %game_dataHandler_public_getItemDisplay({_targetItem})% with command ""/getItem"".")

command /setBankItem [<offline player>] [<text>] [<number>]:
	trigger:	
		if executor is not console:
			if executor is not a op:
				send "&cYou are not allowed to use this command!" to executor
				stop
		if arg 1 or arg 2 is not set:
			send "&cUsage: /setBankItem <player> <ID> [amount]" to executor
			stop
		set {_targetItem} to {-game_dataHandler::%arg 2%}
		if {_targetItem} is not set:
			send "&cNo item found for this ID!" to executor
			stop
		set {_amount} to (check [arg 3 is set] ? (arg 3) : 1)
		if {_amount} < 1:
			set {playerData::%arg 1%_BANK_ITEMS} to stringDatasAPI_removeData({playerData::%arg 1%_BANK_ITEMS}, (arg 2 in upper case))
			send "&aRemoved item from %arg 1%'s bank!" to executor
			game_logHandler_sendLog("%executor% removed item from %arg 1%'s bank with command ""/setBankItem"".")
		else:
			set {playerData::%arg 1%_BANK_ITEMS} to stringDatasAPI_setData((arg 2 in upper case), "%{_amount}%", {playerData::%arg 1%_BANK_ITEMS})
			send "&aAdded item to %arg 1%'s bank!" to executor
			game_logHandler_sendLog("%executor% added item %game_dataHandler_public_getItemDisplay(({_amount} of {_targetItem}))% to %arg 1%'s bank with command ""/setBankItem"".")

command /setQuest [<offline player>] [<text>] [<number>]:
	trigger:	
		if executor is not console:
			if executor is not a op:
				send "&cYou are not allowed to use this command!" to executor
				stop
		if arg 1 or arg 2 or arg 3 is not set:
			send "&cUsage: /setQuest <player> <quest> [level]" to executor
			stop
		set {_level} to (check [arg 3 is set] ? (arg 3) : 0)
		if {-game_dataHandler::%arg 2%_%{_level}%_QUEST_DATA} is not set:
			send "&cInvalid quest name or level!" to executor
			stop
		set {playerData::%arg 1%_COMPLETED_QUEST_DATA} to stringDatasAPI_setData((arg 2 in upper case), "%{_level}%", {playerData::%arg 1%_COMPLETED_QUEST_DATA})
		send "&aSuccessfully set %arg 1%'s quest to level %{_level}%!" to executor
		game_logHandler_sendLog("%executor% set %arg 1%'s %(arg 2 in upper case)% quest to level %{_level}% with command ""/setQuest"".")

command /serverActivity [<offline player>] [<text>]:
	trigger:	
		if executor is not console:
			send "&cYou are not allowed to use this command!" to executor
			stop
		set {_player} to (arg 1)
		set {_playerUUID} to ({_player}'s UUID)
		if (arg 2) is "VOTE" or "BOOST":
			if (arg 2) is "VOTE":
				send "&b%(arg 1)% has voted for the server!" to all players
			else:
				send "&b%(arg 1)% has boosted the server!" to all players
			set {_type} to (arg 2) in upper case
			if (arg 1) is online:
				set {_rewardRaw} to {-game_dataHandler::SERVER_%{_type}%_REWARD}
				give game_dataHandler_public_textToItems({_rewardRaw}, {_player}) to {_player}
				stop
			set {_previousUnclaims} to {gameData::UNCLAIM_%{_type}%S_%{_playerUUID}%}
			set {_previousUnclaims} to (check [{gameData::UNCLAIM_%{_type}%S_%{_playerUUID}%} is set] ? {_previousUnclaims} : 0)
			set {gameData::UNCLAIM_%{_type}%S_%{_playerUUID}%} to ({_previousUnclaims} + 1)
			game_logHandler_sendLog("Unclaim %{_type}% for %{_player}% increase by 1. (TOTAL = %{gameData::UNCLAIM_%{_type}%S_%{_playerUUID}%}%)")

command /wipePlayerData [<offline player>]:
	trigger:	
		if executor is not console:
			if executor is not a op:
				send "&cYou are not allowed to use this command!" to executor
				stop
		if arg 1 is not set:
			send "&cUsage: /wipePlayerData <player>" to executor
			stop
		set {_wipedDataPlayer} to (arg 1)
		delete {playerData::%{_wipedDataPlayer}%_SAVED_INVENTORY}
		delete {playerData::%{_wipedDataPlayer}%_BANK_ITEMS}
		delete {playerData::%{_wipedDataPlayer}%_SAVED_STATS}
		delete {playerData::%{_wipedDataPlayer}%_COMPLETED_QUEST_DATA}
		delete {playerData::%{_wipedDataPlayer}%_ACTIVE_QUEST_DATA}
		delete {-game_questHandler::%{_wipedDataPlayer}%_ACTIVE_QUEST}
		delete {playerData::%{_wipedDataPlayer}%_CODE_DATA}
		# NEED TO EDIT
		set {_playerVaultName} to "%{_wipedDataPlayer}'s UUID%_1"
		delete {game_playerVaultHandler::%{_playerVaultName}%_VAULT}
		delete {playerData::%{_wipedDataPlayer}%_PURCHASED_VAULT}
		if {_wipedDataPlayer} is online:
			clear {_wipedDataPlayer}'s inventory
			game_equipmentHandler_updatePlayerEquipmentStats({_wipedDataPlayer})
			game_playerStatsHandler_loadStats({_wipedDataPlayer})
			game_playerDisplayHandler_sidebar_updatePlayerSidebar({_wipedDataPlayer})
			give {-game_dataHandler::STONE_PICKAXE_1_ITEM} to {_wipedDataPlayer}
		send "&aYou have successfully wiped %{_wipedDataPlayer}%'s data!" to executor
		game_logHandler_sendLog("%executor% wiped %{_wipedDataPlayer}%'s data.")

command /log [<text>]:
	trigger:
		if executor is not a op:
			send "&cYou are not allowed to use this command!" to executor
			stop
		if {-game_eventHandler::%executor%_LOG_TOGGLE} is set:
			delete {-game_eventHandler::%executor%_LOG_TOGGLE}
			send "&eYou have turned &coff &elogs." to executor
			stop
		set {-game_eventHandler::%executor%_LOG_TOGGLE} to now
		send "&eYou have turned &aon &elogs." to executor

command /music [<text>]:
	trigger:
		if {-game_musicHandler::%executor%_MUSIC} is set:
			delete {-game_musicHandler::%executor%_MUSIC}
			execute console command "stopsound %{_player}%"
			send "&eYou have turned &coff &emusic." to executor
			stop
		game_musicHandler_playRepeat(executor, now)
		send "&eYou have turned &aon &emusic." to executor

command /vanish [<text>]:
	aliases: /v
	trigger:
		if executor doesn't have permission "group.helper":
			send "&cYou are not allowed to use this command!" to executor
			stop
		if {-game_vanishHandler::%executor%_VANISH} is set:
			game_vanishHandler_player(executor, false)
			send "&eYou have turned &coff &evanish." to executor
		else:
			game_vanishHandler_player(executor, true)
			send "&eYou have turned &aon &evanish." to executor
		loop all players:
			game_vanishHandler_updateVanishedPlayers(loop-player)

command /nick [<text>]:
	aliases: /v
	trigger:
		if executor doesn't have permission "group.helper":
			send "&cYou are not allowed to use this command!" to executor
			stop
		if arg 1 is not set:
			send "&cUsage: /nick <nickname/clear/off>" to executor
			stop
		set {_executorUUID} to (executor's UUID)
		if arg 1 is "clear" or "off":
			if {playerData::%{_executorUUID}%_NICKNAME} is not set:
				send "&cYou currently don't have any nickname set!" to executor
				stop
			delete {playerData::%{_executorUUID}%_NICKNAME}
		else:
			if game_playerDisplayHandler_checkAlphanumeric(arg 1) is false:
				send "&cYou are only allowed to use letters and numbers in nickname!" to executor
				stop
			if (arg 1) is "%executor%":
				send "&cYour nickname is same as your username!%nl%&cUse ""/nick clear"" to clear your nickname." to executor
				stop
			set {_nicknameSplit::*} to (arg 1) split by ""
			if (amount of {_nicknameSplit::*}) > 16:
				send "&cYou are not allowed to have nickname over 16 characters!" to executor
				stop
			set {playerData::%{_executorUUID}%_NICKNAME} to (arg 1)
		game_healthbarHandler_update(executor)
		game_playerDisplayHandler_playerTablist(executor)
		send "&eYour display name is now &6%game_playerDisplayHandler_getPlayerName(executor)%&e." to executor

command /resetMines [<text>]:
	trigger:
		if executor is not a op:
			send "&cYou are not allowed to use this command!" to executor
			stop
		game_mineResetHandler_resetAllAreas()
		send "&d%executor% has forced all mines to reset!" to all players

command /beez-vote [<text>]:
	trigger:
		send "&bVote for us at <url:https://playerservers.com/server/Beez>https://playerservers.com/server/Beez<reset>&b." to executor

command /announcement [<text>]:
	trigger:
		if executor is not a op:
			send "&cYou are not allowed to use this command!" to executor
			stop
		if arg 1 is not set:
			send "&cUsage: /announcement <message>" to executor
			stop
		send "&b[Announcement] %arg 1%" to all players

command /getGlitchedPlayerQuestData [<text>]:
	trigger:
		if executor is not a op:
			send "&cYou are not allowed to use this command!" to executor
			stop
		set {_loopCount} to 0
		loop all offline players:
			add 1 to {_loopCount}
			set {_loopPlayer} to loop-value
			if {_loopPlayer} is online:
				game_questHandler_saveData({_loopPlayer})
			set {_loopPlayerActiveQuest} to stringDatasAPI_getData({playerData::%{_loopPlayer}%_ACTIVE_QUEST_DATA}, "ACTIVE_QUEST")
			if {_loopPlayerActiveQuest} is not set:
				set {_invalid} to true
			set {_loopPlayerActiveQuestSplit::*} to {_loopPlayerActiveQuest} split by "_"
			set {_loopPlayerActiveQuestSplitAmount} to (amount of {_loopPlayerActiveQuestSplit::*})
			set {_loopPlayerActiveQuestLevel} to ({_loopPlayerActiveQuestSplit::%{_loopPlayerActiveQuestSplitAmount}%} parsed as number)
			delete {_loopPlayerActiveQuestSplit::%{_loopPlayerActiveQuestSplitAmount}%}
			set {_loopPlayerActiveQuestType} to join {_loopPlayerActiveQuestSplit::*} with delimiter "_"
			set {_playerQuestCompletedLevel} to (stringDatasAPI_getData({playerData::%{_loopPlayer}%_COMPLETED_QUEST_DATA}, {_loopPlayerActiveQuestType}) parsed as number)
			if {_playerQuestCompletedLevel} is not set:
				set {_invalid} to true
			if {_invalid} is not set:
				if {_loopPlayerActiveQuestLevel} <= {_playerQuestCompletedLevel}:
					send "%{_loopPlayer}% &c- &fC=%{_playerQuestCompletedLevel}% &c- &fA=%{_loopPlayerActiveQuestLevel}%%nl%&cDATA-C: &f%{playerData::%{_loopPlayer}%_COMPLETED_QUEST_DATA}%%nl%&cDATA-A: &f%{playerData::%{_loopPlayer}%_ACTIVE_QUEST_DATA}%" to executor
			delete {_loopPlayerActiveQuest}
			delete {_playerQuestCompletedLevel}
			delete {_invalid}
		if ({_loopCount} / 30) is floor({_loopCount} / 30):
			wait 1 tick

command /guarenteeBossFightEvent [<text>]:
	trigger:
		if executor is not a op:
			send "&cYou are not allowed to use this command!" to executor
			stop
		if {-game_eventHandler::GUARENTEE_BOSS_FIGHT_EVENT} is set:
			send "&cBoss fight event is alraedy guarentee in less than 6 minutes!" to executor
			stop
		set {-game_eventHandler::GUARENTEE_BOSS_FIGHT_EVENT} to true
		send "&eYou have set next boss fight event to be guarenteed!" to executor

every 1 second:
	set {-game_eventHandler::MINE_RESET_COUNTDOWN} to (check [{-game_eventHandler::MINE_RESET_COUNTDOWN} is set] ? {-game_eventHandler::MINE_RESET_COUNTDOWN} : 121)
	remove 1 from {-game_eventHandler::MINE_RESET_COUNTDOWN}
	if {-game_eventHandler::MINE_RESET_COUNTDOWN} <= 0:
		set {-game_eventHandler::MINE_RESET_COUNTDOWN} to 16
		loop all players:
			if game_dataHandler_public_checkInArea((loop-player's location), "ORE_MINES") is true:
				game_healthbarHandler_teleport(loop-player, {-game_dataHandler::SPAWN_LOCATION})
		game_mineResetHandler_resetAllAreas()
		send "&eAll mines have been reset!%nl%&eThe next mine reset is coming in 15 seconds!" to all players
#	else if {-game_eventHandler::MINE_RESET_COUNTDOWN} is 30 or 20 or 10 or 5:
#		send "&eAll mines is resetting in %{-game_eventHandler::MINE_RESET_COUNTDOWN}% seconds!" to all players
	loop all players:
		game_healthHandler_healthRegeneration(loop-player)
		game_playerDisplayHandler_playerActionbar(loop-player)

every 1 minute:
	send "&6All players inventory data have been saved." to all players
	loop all players:
		game_playerInventoryHandler_saveInventory(loop-player)
		game_playerDisplayHandler_playerTablist(loop-player)
		game_playerDisplayHandler_sidebar_updatePlayerSidebar(loop-player)

every 3 minutes:
	set {_autoBroadcastRandomID} to (random integer between 1 and {-game_dataHandler::AUTO_BROADCAST_AMOUNT})
	send "&b[TIP] %{-game_dataHandler::AUTO_BROADCAST_MESSAGE_%{_autoBroadcastRandomID}%}%" to all players
	set {_airdropButtonChance} to (random integer between 1 and 5)
	if {_airdropButtonChance} is 1:
		game_randomEventHandler_airDrop_spawnButtonRandomLocation()
	game_leaderboardHandler_updateTopMinedBlocksLeaderboard()

every 30 seconds:
	game_eventHandler_updateAllHealthbar()

every 6 minutes:
	set {_bossFightChance} to (random integer between 1 and 4)
	if {-game_eventHandler::GUARENTEE_BOSS_FIGHT_EVENT} is set:
		delete {-game_eventHandler::GUARENTEE_BOSS_FIGHT_EVENT}
		game_randomEventHandler_bossFight_newBossFight("EVENT_BOSS_1", "TICKET_ITEM:1~2,ROTTEN_BOOTS_1_ITEM:1:1")
	else if {_bossFightChance} is 1:
		game_randomEventHandler_bossFight_newBossFight("EVENT_BOSS_1", "TICKET_ITEM:1~2,ROTTEN_BOOTS_1_ITEM:1:1")

on block break:
	set {_playerTool} to player's tool
	if game_temporaryBlockHandler_isTemporaryBlock(event-block) is true:
		game_temporaryBlockHandler_breakBlock(player, event-block)
		stop
	if player's gamemode is creative:
		stop
	if game_randomEventHandler_airDrop_checkValid(event-block's location) is true:
		game_randomEventHandler_airDrop_playerOpenAirdrop(player, (event-block's location))
		stop
	if game_dataHandler_oreBlocks_checkValid(event-block's type) is false:
		cancel event
		stop
	if game_dataHandler_public_checkInArea((event-block's location), "ORE_MINES") is false:
		cancel event
		stop
	if game_itemDataHandler_itemType_getType({_playerTool}) is not "PICKAXE" or "AXE":
		cancel event
		stop
	if game_pickaxeHandler_pickaxe_checkBreakable(event-block's type, {_playerTool}) is false:
		cancel event
		send "&cYour tool can't break this ore!" to player
		stop
	game_oreHandler_oreBreak(player, event-block)

on block place:
	if player's gamemode is creative:
		stop
	if event-block is not white wool:
		cancel event
		stop
	if game_dataHandler_public_checkInArea((event-block's location), ("SPAWN" and "ORE_MINES")) is true:
		send "&cYou are not allowed to place blocks inside this area!" to player
		cancel event
		stop
	if event-block's y coords < 95:
		set {_insideRestrictArea} to true
	else if event-block's y coords > 110:
		set {_insideRestrictArea} to true
	if {_insideRestrictArea} is set:
		send "&cYou are only allowed to place blocks in range of 95~110 y coords." to player
		cancel event
		stop
	game_temporaryBlockHandler_registerBlock(player, event-block)
	game_questHandler_recordStat(player, "PLACING_Temporary Block", 1)

on rightclick:
	set {_clickedSlot} to player's current hotbar slot
	set {_clickedTool} to player's tool
	if player's gamemode is creative:
		stop
	else if {_clickedTool} is bow:
		stop
	set {_clickedToolType} to game_itemDataHandler_itemType_getType({_clickedTool})
	if {_clickedToolType} is "PICKAXE" or "AXE" or "ARMOR" or "WEAPON":
		game_shopHandler_itemUpgrade_gui(player, {_clickedTool}, {_clickedSlot})
	else if clicked block is set:
		if clicked block is ender chest:
			game_crateHandler_gui(player)
		else if clicked block is ENDER_PORTAL_FRAME:
			if {game_playerVaultHandler::%player%_PLAYER_INTERACTING_VAULT} is set:
				stop
			game_playerVaultHandler_playerInteractVault(player, (clicked block's location))
			play sound "block.dispenser.fail" with volume 99999 and pitch 1 at (player's location) for player
		else if "%clicked block's location%" is "%{gameData::AIRDROP_BUTTON_LOCATION}%":
			set clicked block to air
			delete {gameData::AIRDROP_BUTTON_LOCATION}
			game_randomEventHandler_airDrop_startAirdrop(player, "METEOR")
		else:
			stop
	else:
		stop
	cancel event

on inventory click:
	if clicked slot is 40:
		if "%inventory name of current inventory of player%" is not "Player Vault":
			cancel event
			stop
	if inventory name of current inventory of player is "container.crafting":
		# nothing
#	else if "%clicked inventory%" is not "inventory of %player%":
#		# nothing
	else if "%inventory name of current inventory of player%" is "Player Vault":
		# nothing
	else:
#		send "&cYou are not allowed to interact with your inventory at this time!" to player
		cancel event
	wait 1 tick
	game_equipmentHandler_updatePlayerEquipmentStats(player)

on inventory close:
	if {game_playerVaultHandler::%player%_PLAYER_INTERACTING_VAULT} is not set:
		stop
	game_playerVaultHandler_saveVault({game_playerVaultHandler::%player%_PLAYER_INTERACTING_VAULT}, event-inventory)
	delete {game_playerVaultHandler::%player%_PLAYER_INTERACTING_VAULT}
	play sound "block.dispenser.fail" with volume 99999 and pitch 1 at (player's location) for player

on drop:
#	if game_itemDataHandler_itemType_getType(event-item) is "PICKAXE" or "AXE":
#		cancel event
#		send "&cYou can't drop your pickaxe!" to player
#		stop
#	if inventory name of current inventory of player is "container.crafting":
#		game_equipmentHandler_updatePlayerEquipmentStats(player)
#		stop
	cancel event

on pickup:
	cancel event

on chat:
	set {_playerUUID} to (player's UUID)
	set {_playerMessageSplit::*} to message split by " "
	loop {_playerMessageSplit::*}:
		set {_withoutBox} to {_playerMessageSplit::%loop-index%}
		replace all "[" and "]" with "" in {_withoutBox}
		set {_commandSplit::*} to {_withoutBox} split by ":"
		if {_playerMessageSplit::%loop-index%} is not "[%{_withoutBox}%]":
			# nothing
		else if {_commandSplit::1} is "TOOL" or "ITEM":
			if player's tool is not air:
				if (amount of {_commandSplit::*}) is 1:
					set {_playerMessageSplit::%loop-index%} to "&c[Tool: %game_dataHandler_public_getItemDisplay(player's tool)%]&f"
		else if {_commandSplit::1} is "BANK":
			if (amount of {_commandSplit::*}) is not 2:
				set {_illegalFormula} to true
			else if {-game_dataHandler::%{_commandSplit::2}%} is not set:
				set {_illegalFormula} to true
			if {_illegalFormula} is not set:
				set {_itemsInBank} to (stringDatasAPI_getData({playerData::%player%_BANK_ITEMS}, {_commandSplit::2}) parsed as number)
				set {_itemsInBank} to (check [{_itemsInBank} is set] ? {_itemsInBank} : 0)
				set {_displayItem} to ({_itemsInBank} of {-game_dataHandler::%{_commandSplit::2}%})
				set {_playerMessageSplit::%loop-index%} to "&c[Bank: %game_dataHandler_public_getItemDisplay({_displayItem})%]&f"
		else if {_commandSplit::1} is "STAT":
			if (amount of {_commandSplit::*}) is not 2:
				set {_illegalFormula} to true
			else if {_commandSplit::2} is not "KILLS" or "DEATHS" or "MINED_BLOCKS":
				set {_illegalFormula} to true
			if {_illegalFormula} is not set:
				if {_commandSplit::2} is "KILLS":
					set {_statDisplay} to "Kills"
				else if {_commandSplit::2} is "DEATHS":
					set {_statDisplay} to "Deaths"
				else if {_commandSplit::2} is "MINED_BLOCKS":
					set {_statDisplay} to "Mined Blocks"
				set {_playerMessageSplit::%loop-index%} to "&c[%{_statDisplay}%: %{-game_playerStatsHandler::%player%_%{_commandSplit::2}%}%]&f"
		delete {_itemsInBank}
		delete {_illegalFormula}
	set {_playerMessage} to join {_playerMessageSplit::*} with delimiter " "
	if {-game_leaderboardHandler_playerRanked::%{_playerUUID}%} is set:
		set {_leaderboardRankedDisplay} to " &e«&f##%{-game_leaderboardHandler_playerRanked::%{_playerUUID}%}%&e»"
	else:
		set {_leaderboardRankedDisplay} to ""
	if player is a op:
		if {_playerMessage} contains "&":
			set {_colorcodeCheck::*} to {_playerMessage} split by ""
			loop (amount of {_colorcodeCheck::*}) times:
				if {_colorcodeCheck::%loop-number%} is "&":
					set {_nextCharNum} to (loop-number + 1)
					# I dont want text style like bold or something, I want color only.
					if {_colorcodeCheck::%{_nextCharNum}%} is "0" or "1" or "2" or "3" or "4" or "5" or "6" or "7" or "8" or "9" or "a" or "b" or "c" or "d" or "e" or "f":
						set {_colorcodeCheck::%loop-number%} to "§"
			set {_playerMessage} to join {_colorcodeCheck::*} with delimiter ""
	set chat format to "%player's prefix%%game_playerDisplayHandler_getPlayerName(player)%%{_leaderboardRankedDisplay}% &f%{_playerMessage}%"

on script load:
	delete {-game_eventHandler::LOADED}
	wipe server crafting recipes
	wait 2 seconds
	kill all dropped items
	wait 4 seconds
	set {-game_eventHandler::LOADED} to true

on script unload:
	loop all players:
		game_questHandler_saveData(loop-player)
		game_playerInventoryHandler_saveInventory(loop-player)
		game_playerStatsHandler_saveStats(loop-player)

on join:
	if player's uuid is "2bbd185f-8124-4339-b5ae-db9a4c614fd5":
		set join message to ""
	else:
		set join message to "&2&lJOIN! &a%player%"
	wait 5 ticks
	game_playerInventoryHandler_loadInventory(player)
	game_playerStatsHandler_loadStats(player)
	game_questHandler_loadData(player)
	game_playerDisplayHandler_playerTablist(player)
	game_playerDisplayHandler_sidebar_newPlayerSidebar(player)
	game_playerDisplayHandler_sidebar_updatePlayerSidebar(player)
	game_healthbarHandler_new(player)
	game_healthbarHandler_update(player)
	if {playerData::%player%_SAVED_INVENTORY} is not set:
		give player {-game_dataHandler::STONE_PICKAXE_1_ITEM}
	game_invisiblePressurePlateHandler_hideAllFromPlayer(player)
	if {playerData::%player%_PERSONAL_BOOSTER_TYPE} is set:
		game_boosterHandler_countdown(player, {playerData::%player%_PERSONAL_BOOSTER_ID})
	game_healthbarHandler_teleport(player, {-game_dataHandler::SPAWN_LOCATION})
	set {_playerUUID} to (player's UUID)
	set {_checkUnclaimTypes::*} to "VOTE" and "BOOST"
	loop {_checkUnclaimTypes::*}:
		set {_loopType} to {_checkUnclaimTypes::%loop-index%}
		if {gameData::UNCLAIM_%{_loopType}%S_%{_playerUUID}%} is set:
			set {_announeUnclaim} to true
			set {_unclaim%{_loopType}%s} to {gameData::UNCLAIM_%{_loopType}%S_%{_playerUUID}%}
			loop {_unclaim%{_loopType}%s} times:
				set {_rewardRaw} to {-game_dataHandler::SERVER_%{_loopType}%_REWARD}
				give game_dataHandler_public_textToItems({_rewardRaw}, player) to player
			delete {gameData::UNCLAIM_%{_loopType}%S_%{_playerUUID}%}
		else:
			set {_unclaim%{_loopType}%s} to 0
	if {_announeUnclaim} is true:
		send "&bYou have received your reward for %{_unclaimVOTEs}% votes and %{_unclaimBOOSTs}% boosts!" to player
	game_vanishHandler_updateVanishedPlayers(player)
	while (true):
		if {-game_eventHandler::LOADED} is set:
			exit 1 loop
		wait 1 second
	game_musicHandler_playRepeat(player, now)
	if player is a op:
		wait 2 seconds
		set {-game_eventHandler::%player%_LOG_TOGGLE} to now
		send "&f%nl%&cYour log have been automatically turned on as you are opped.%nl%&cUse command ""/log"" to toggle your log.%nl%&f" to player

on quit:
	if player's uuid is "2bbd185f-8124-4339-b5ae-db9a4c614fd5":
		set quit message to ""
	else:
		set quit message to "&4&lQUIT! &c%player%"
	delete {-game_playerDisplayHandler::%player%_SIDEBAR_LOADED}
	game_playerInventoryHandler_saveInventory(player)
	game_playerStatsHandler_saveStats(player)
	game_questHandler_saveData(player)
	game_healthbarHandler_remove(player)
	set {_currentBossFightID} to {-game_bossFightHandler::%player%_CURRENT_BOSS_FIGHT}
	if {_currentBossFightID} is set:
		delete {-game_bossFightHandler::%player%_CURRENT_BOSS_FIGHT}
		remove 1 from {-game_bossFightHandler::%{_currentBossFightID}%_PLAYERS_AMOUNT}
	game_bossFightHandler_sendMessageToBossFightPlayers({_currentBossFightID}, "%player% has left! (%({-game_bossFightHandler::%{_currentBossFightID}%_PLAYERS_AMOUNT} - 1)% Players Remaining)")

on item spawn:
	cancel event

on craft:
	cancel event

on hunger meter change:
	cancel event

on shoot:
	set {_arrowUUID} to (projectile's UUID)
	set {_arrowType} to game_bowHandler_arrowRaritySelector(shooter)
	set {_arrowItem} to {-game_dataHandler::%{_arrowType}%}
	set {_arrowData::*} to game_itemDataHandler_itemData_getDatas({_arrowItem}, "RANGED DAMAGE")
	set {_arrowData::1} to (check [{_arrowData::1} is set] ? ({_arrowData::1} parsed as number) : 0)
	set {-game_eventHandler::PROJECTILE_%{_arrowUUID}%} to ({playerData::%shooter%_RANGED_DAMAGE} + {_arrowData::1})
	if game_eventHandler_checkArrowReturn(shooter) is false:
		remove (1 of {_arrowItem}) from shooter's inventory
	game_questHandler_recordStat(shooter, "SHOOTING_%{_arrowType}%", 1)

on damage:
	set damage to 0
	if victim is armor stand:
		stop
	if attacker and victim is a player:
		if game_dataHandler_public_checkInArea((attacker's location), "SPAWN") or game_dataHandler_public_checkInArea((victim's location), "SPAWN") is true:
			cancel event
			stop
	if attacker is a player:
		if victim is not a player:
			set {_victimUUID} to (victim's UUID)
			if (amount of {-game_mobHandler::%{_victimUUID}%_TARGETS::*}) <= 0:
				set {_triggerTargetFunction} to true
			if {-game_mobHandler::%{_victimUUID}%_TARGETS::%attacker%} is not set:
				set {-game_mobHandler::%{_victimUUID}%_TARGETS::%attacker%} to attacker
				set {-game_mobHandler::%{_victimUUID}%_DAMAGE_TAKEN::%attacker%} to (check [{-game_mobHandler::%{_victimUUID}%_DAMAGE_TAKEN::%attacker%} is set] ? {-game_mobHandler::%{_victimUUID}%_DAMAGE_TAKEN::%attacker%} : 0)
			if {_triggerTargetFunction} is set:
				game_mobHandler_target(victim, {-entityData::%{_victimUUID}%_DEATH_COUNT})
	set {_damageCause} to "%damage cause%"
	if {_damageCause} is "projectile":
		set {_projectileUUID} to (projectile's UUID)
		if {-game_eventHandler::PROJECTILE_%{_projectileUUID}%} is set:
			game_healthHandler_damage(attacker, victim, {-game_eventHandler::PROJECTILE_%{_projectileUUID}%}, "RANGED_DAMAGE")
			delete {-game_eventHandler::PROJECTILE_%{_projectileUUID}%}
	else if {_damageCause} is "attack":
		game_healthHandler_damage(attacker, victim, {playerData::%attacker%_MELEE_DAMAGE}, "MELEE_DAMAGE")
	else if {_damageCause} is "void":
		game_healthHandler_death({_null}, victim)
		stop
	else if {_damageCause} is "suffocation":
		if victim is not a player:
			set {_victimUUID} to ({_victim}'s UUID)
			set {-game_eventHandler::%{_victimUUID}%_MOB_SUFFOCATION_COUNT} to (check [{-game_eventHandler::%{_victimUUID}%_MOB_SUFFOCATION_COUNT} is set] ? {-game_eventHandler::%{_victimUUID}%_MOB_SUFFOCATION_COUNT} : 0)
			add 1 to {-game_eventHandler::%{_victimUUID}%_MOB_SUFFOCATION_COUNT}
			if {-game_eventHandler::%{_victimUUID}%_MOB_SUFFOCATION_COUNT} >= 10:
				game_mobHandler_respawnMob(victim)
	set damage delay of victim to 1 tick

on swap hand item:
	cancel event

on tool change:
	wait 1 tick
	game_equipmentHandler_updatePlayerEquipmentStats(player)

on heal:
	cancel event

on projectile hit:
	kill event-projectile

on xp spawn:
	cancel event

on step on pressure plate:
	game_invisiblePressurePlateHandler_runAction(player, (event-block's location))

on spawn:
	set {_spawnReason} to "%spawn reason%"
	if {_spawnReason} is "BREEDING":
		cancel event

function game_eventHandler_checkArrowReturn(player: player) :: boolean:
	set {_arrowReturnChance} to {playerData::%{_player}%_ARROW_RETURN}
	set {_random} to (random number between 0 and 100)
	if {_arrowReturnChance} >= {_random}:
		return true
	return false

function game_eventHandler_updateAllHealthbar():
	kill all area effect clouds
	set {_mobUUID::*} to {-game_dataHandler::MOB_LIST} split by ":"
	add (all players) to {_healthbarUpdateEntityList::*}
	loop {_mobUUID::*}:
		set {_loopMobUUID} to {_mobUUID::%loop-index%}
		set {_loopMob} to {-entityData::%{_loopMobUUID}%_ENTITY}
		add {_loopMob} to {_healthbarUpdateEntityList::*}
	set {_loopCount} to 0
	loop {_healthbarUpdateEntityList::*}:
		add 1 to {_loopCount}
		game_healthbarHandler_new(loop-value)
		game_healthbarHandler_update(loop-value)
		if ({_loopCount} / 5) is floor({_loopCount} / 5):
			wait 1 tick